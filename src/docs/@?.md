    @? expr
    @? :debug expr

"Lift" functions to operate on `Union{Some{T}, Nothing}`.

Idea:

* Each function `f(::T₁, ..., ::Tₙ) -> ::R` is (conceptually) lifted to
  `f′(::Union{Some{T₁}, Nothing}, ..., ::Union{Some{Tₙ}, Nothing})
  -> ::Union{Some{R}, R, Nothing}`.
* If any of the returned value is `nothing`, the evaluation of `expr`
  in `@?` short-circuits and evaluates to `nothing`.

**Debugging support** (experimental): In the two-argument form `@?
:debug expr` (`:debug` is the literal symbol `:debug`), `@?` inserts
`@debug` logging statements for all possible short-circuiting points.
This logging statement prints the expression that is evaluated to
`nothing` and all the local variables using `Base.@locals`.  This
macro also inserts a call to a no-op function `Maybe._break`.  When
using Debugger.jl, the state just before exit can be examined by
adding it to the breakpoint with `bp add Maybe._break`.

See the [tutorial](@ref) for more information.

# Examples
```jldoctest
julia> using Maybe

julia> dict = Dict(:a => Dict(:b => 1));

julia> @? dict[:a][:b] + 2
Some(3)

julia> @? dict[:a][:b] + dict[:c]  # => nothing
```

The expression in `@?` short-circuits when a call returns `nothing`:

```jldoctest; setup = :(using Maybe)
julia> f() = nothing;

julia> @? println(f())
```

`Some` is unwrapped when it appears in the argument position:

```jldoctest; setup = :(using Maybe)
julia> f() = Some("hello");

julia> @? println(f())
hello
```

Non-`nothing` returned value is always ensured to be wrapped in `Some`
at the end

```jldoctest; setup = :(using Maybe)
julia> f() = "hello";

julia> @? f()
Some("hello")

julia> g() = Some("hello");

julia> @? g()
Some("hello")
```

`nothing` literal is always wrapped

```jldoctest; setup = :(using Maybe)
julia> @? Some(nothing)
Some(nothing)
```

`return $expr` does not return when `$expr` evaluates to `nothing`

```jldoctest; setup = :(using Maybe)
julia> f() = (@? return identity(nothing); return 1);

julia> f()
1
```

`return $expr` returns `Some($expr)`:

```jldoctest; setup = :(using Maybe)
julia> @? 1 + 1
Some(2)

julia> f() = @? return 1 + 1;

julia> f()
Some(2)
```

`$(...)` can be used for switching to the normal evaluation

```jldoctest; setup = :(using Maybe)
julia> @? Some(Some(Some(1)))
Some(1)

julia> @? $(Some(Some(Some(1))))
Some(Some(Some(Some(1))))
```

`$f(...)` can be used for extra wrapping with `Some`

```jldoctest; setup = :(using Maybe)
julia> f() = Some("hello");

julia> @? f()
Some("hello")

julia> @? $f()
Some(Some("hello"))
```

`@?` recursively transforms functions

```jldoctest; setup = :(using Maybe)
julia> @? f(dict, i, j, k) =
           get(dict, i) do
               dict[j] + dict[k]
           end;

julia> f(Dict(), :a, :b, :c)

julia> f(Dict(:a => 1), :a, :b, :c)
Some(1)

julia> f(Dict(:b => 1), :a, :b, :c)

julia> f(Dict(:b => 1, :c => 2), :a, :b, :c)
Some(3)
```
